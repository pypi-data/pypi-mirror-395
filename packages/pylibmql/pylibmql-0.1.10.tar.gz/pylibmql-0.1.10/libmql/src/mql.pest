WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT = _{ "//" ~ (!"\n" ~ ANY)* }

select = { "SELECT" }
limit = { "LIMIT" }
from = { "FROM" }

quantity_single = @{ ASCII_DIGIT+ }
quantity_many = ${ quantity_single ~ "-" ~ quantity_single }
quantity = { quantity_many | quantity_single }

class = @{ "CLASS" | "class" }
placement = @{ "PLACEMENT" | "placement" }
tag = @{ "TAG" | "tag" }
range = @{ "RANGE" | "range" }
tag_dept = @{ "TAG_DEPT" | "tag_dept" }
dist = @{ "DIST" | "dist" }
dist_dept = @{ "DIST_DEPT" | "dist_dept" }
range_dist = @{ "RANGE_DIST" | "range_dist" }
range_tag = @{ "RANGE_TAG" | "range_tag" }

bad_query = @{ (ASCII_ALPHANUMERIC | "_")+ }

query_name = ${ (class | placement | tag_dept | tag | dist_dept | dist | range_dist | range_tag | range) 
  ~ ! (ASCII_ALPHANUMERIC | "_") | bad_query }

string = @{ "\"" ~ ( "\"\"" | (!("\"" | "\r" | "\n") ~ (ANY)) )* ~ "\"" }

department_id_char = _{ ASCII_ALPHA_UPPER | "&" }

bad_department_id = { ASCII_ALPHA{5,} | ASCII_ALPHA{1,2} }
department_id = { department_id_char{3,4} }

class_id = ${ ASCII_DIGIT{4} ~ "L"? }

class_argument = ${ (bad_department_id ~ (" ")* ~ class_id) | (department_id ~ (" ")* ~ class_id) }

query_argument = { string | class_argument }

lpar = _{ "(" }
rpar = _{ ")" }

XYZ = { query_name ~ lpar ~ (query_argument ~ ("," ~ query_argument)*) ~ rpar }

selector_list = { "[" ~ selector_single ~ ("," ~ selector_single)* ~ "]" }
selector_single = { statement | XYZ }

selector = { selector_list | selector_single }

semicolon = _{ ";" }

statement = !{ (select | limit) ~ quantity ~ from ~ selector }

sep = _{ ":"}

description = _{ (sep ~ string) }
priority = _{ (sep ~ quantity_single) }

special_statement = { statement ~ description ~ priority? }

file = { SOI ~ (special_statement ~ semicolon)* ~ EOI  }